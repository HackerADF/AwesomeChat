plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.3'
}

group = 'dev.adf'
version = '1.0.6-DEV'

repositories {
    mavenCentral()

    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "bstats"
        url = "https://repo.extendedclip.com/content/repositories/bstats/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        url = "https://repo.codemc.org/repository/maven-public/"
    }
    maven {
        url = "https://jitpack.io"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT")
    compileOnly("org.bstats:bstats-bukkit:3.1.0")
    compileOnly("net.luckperms:api:5.4")
    compileOnly("me.clip:placeholderapi:2.11.6")
    compileOnly("com.github.DevLeoko:AdvancedBan:v2.3.0") // will be adding hooks to advancedban and lightbans, can remove for now.
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching("plugin.yml") {
        expand props
    }
}

shadowJar {
    archiveClassifier.set("") // optional, was using for testing
}
